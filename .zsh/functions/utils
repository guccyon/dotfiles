dropbox_remove_conflict() {
  find ~/Dropbox/ -name "*conflict*" -exec rm {} \;
}

function ls_abbrev() {
    # -a : Do not ignore entries starting with ..
    # -C : Force multi-column output.
    # -F : Append indicator (one of */=>@|) to entries.
    local cmd_ls='ls'
    local -a opt_ls
    opt_ls=('-CF' '--color=always')
    case "${OSTYPE}" in
        freebsd*|darwin*)
            if type gls > /dev/null 2>&1; then
                cmd_ls='gls'
            else
                # -G : Enable colorized output.
                opt_ls=('-aCFG')
            fi
            ;;
    esac

    local ls_result
    ls_result=$(CLICOLOR_FORCE=1 COLUMNS=$COLUMNS command $cmd_ls ${opt_ls[@]} | sed $'/^\e\[[0-9;]*m$/d')

    local ls_lines=$(echo "$ls_result" | wc -l | tr -d ' ')

    if [ $ls_lines -gt 10 ]; then
        echo "$ls_result" | head -n 5
        echo '...'
        echo "$ls_result" | tail -n 5
        echo "$(command ls -1 -A | wc -l | tr -d ' ') files exist"
    else
        echo "$ls_result"
    fi
}


function do_enter() {
  if [ -n "$BUFFER" ]; then
    zle accept-line
    return 0
  fi
  echo
  ls
  # ls_abbrev
  if [ "$(git rev-parse --is-inside-work-tree 2> /dev/null)" = 'true' ]; then
      echo
      echo -e "\e[0;33m--- git status ---\e[0m"
      git status -sb
  fi
  zle reset-prompt
  return 0
}

nprom () {
    setopt prompt_subst
    local rbase=$'%{\e[33m%}[%~]%{\e[m%}' lf=$'\n'
    local pct=$'%0(?||%18(?||%{\e[31m%}))%#%{\e[m%}'
    RPROMPT="%9(~||$rbase)"
    case "$USER" in
        yatex)    PROMPT=$'%{\e[33m%}%U%m{%n}%%%{\e[m%}%u ' ;;
        java)     PROMPT=$'%{\e[36m%}%U%m{%n}%%%{\e[m%}%u ' ;;
        *)
            local pbase=$'%{\e[$[32+RANDOM%5]m%}%B%n@%m%b'"$pct "
            PROMPT="%9(~|$rbase$lf|)$pbase"
            ;;
    esac
    [[ "$TERM" = "screen" ]] && RPROMPT="[%~]"
}
gitbr () {
	git fetch origin
	git co -b "$1" origin/master
	git subup
	xcode
}

gitgc () {
	git remote prune origin
	git gc
}

gitcoo () {
	git fetch origin
	git co "origin/$1"
	git subup
	xcode
}

xcode () {
    if ls *.xcworkspace > /dev/null 2>&1
    then
        find . -maxdepth 1 -name '*.xcworkspace' -exec open '{}' \;
    elif ls *.xcodeproj > /dev/null 2>&1
    then
        find . -maxdepth 1 -name '*.xcodeproj' -exec open '{}' \;
    fi
}

function peco-src () {
    local selected_dir=$(ghq list --full-path | peco --query "$LBUFFER")
    if [ -n "$selected_dir" ]; then
        BUFFER="cd ${selected_dir}"
        zle accept-line
    fi
    zle clear-screen
}
zle -N peco-src


function peco-select-history() {
    local tac
    if which tac > /dev/null; then
        tac="tac"
    else
        tac="tail -r"
    fi
    BUFFER=$(history -n 1 | \
        eval $tac | \
        peco --query "$LBUFFER")
    CURSOR=$#BUFFER
    zle clear-screen
}
zle -N peco-select-history

precmd () {
  LANG=en_US.UTF-8 vcs_info
  PROMPT='%{$fg_bold[white]%}$BASE_FORMAT${vcs_info_msg_0_}%{$fg_bold[white]%}%%%{$reset_color%} '
}
